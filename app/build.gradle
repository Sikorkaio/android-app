apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

// load api key from properties file
def properties = new Properties()
file("$rootDir/api-keys.properties").withInputStream { properties.load(it) }

static def version() {
  return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def revision() {
  return 'git rev-list --count HEAD'.execute().text.trim()
}

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'
  defaultConfig {
    applicationId "io.sikorka.android"
    minSdkVersion 17
    targetSdkVersion 27
    versionCode 1
    versionName "1.0.0-alpha.1"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true

    resValue "string", "maps_key", properties["google_maps_key"]

    buildConfigField "String", "GIT_HASH", "\"${version()}\""
    buildConfigField "String", "REVISION", "\"${revision()}\""


  }

  signingConfigs {
    debug {
      storeFile file("$rootDir/debug.keystore")
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  android.applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "${defaultConfig.applicationId}-${variant.versionCode}-v${variant.versionName}.apk"
    }
  }

}

ext.versions = [
    support_library_version: '27.0.2',
    play_services_version  : '11.8.0',
    room_version           : '1.0.0'
]

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "com.android.support:support-v4:${versions.support_library_version}"
  implementation "com.android.support:support-v13:${versions.support_library_version}"
  implementation "com.android.support:support-vector-drawable:${versions.support_library_version}"
  androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })

  // Support libraries
  implementation "com.android.support:appcompat-v7:${versions.support_library_version}"
  implementation "com.android.support:recyclerview-v7:${versions.support_library_version}"
  implementation "com.android.support:design:${versions.support_library_version}"
  implementation "com.android.support:preference-v7:${versions.support_library_version}"
  implementation "com.android.support:preference-v14:${versions.support_library_version}"

  implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta4'

  // Custom build of master with an addition to an unmarshal issue
  // https://github.com/ethereum/go-ethereum/issues/14832#issuecomment-322965627
  // build of 049797d40 master (plus mobile single unmarshal fix)
  implementation 'geth-android:geth:1.7.3-unstable'

  implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'
  implementation 'com.squareup.okio:okio:1.13.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  //reactive extensions
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
// Because RxAndroid releases are few and far between, it is recommended you also
// explicitly depend on RxJava's latest version for bug fixes and new features.
  implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

  implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

  // Apache commons IO
  implementation group: 'commons-io', name: 'commons-io', version: '2.5'

  //dependency injection
  implementation 'com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.1'
  implementation 'com.github.stephanenicolas.toothpick:smoothie:1.1.1'
  compileOnly 'javax.annotation:jsr250-api:1.0'

  kapt 'com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.1'

  //android persistance (SQLite)

  // Database
  implementation "android.arch.persistence.room:runtime:${versions.room_version}"
  kapt "android.arch.persistence.room:compiler:${versions.room_version}"
  implementation "android.arch.persistence.room:rxjava2:${versions.room_version}"

  implementation "android.arch.paging:runtime:1.0.0-alpha4-1"

  implementation "android.arch.lifecycle:extensions:1.1.0"

  implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
  implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

  implementation 'com.github.apl-devs:appintro:v4.2.2'

  //Logging
  implementation 'com.jakewharton.timber:timber:4.6.0'

  implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

  //Location
  implementation "com.google.android.gms:play-services-gcm:${versions.play_services_version}"
  implementation "com.google.android.gms:play-services-location:${versions.play_services_version}"
  implementation "com.google.android.gms:play-services-maps:${versions.play_services_version}"

  //QR support
  implementation 'me.dm7.barcodescanner:zxing:1.9.8'

  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

  //Unit testing
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.13.0'
  testImplementation 'com.github.stephanenicolas.toothpick:toothpick-testing:1.1.1'
  testImplementation "com.google.truth:truth:0.39"
  testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
  testImplementation "android.arch.core:core-testing:1.1.0"
}


kapt {
  arguments {
    arg('toothpick_registry_package_name', 'io.sikorka.android.di')
    arg("room.schemaLocation", "$projectDir/schemas".toString())
  }
}
